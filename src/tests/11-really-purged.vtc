varnishtest "Test softpurge vmod"

server s1 {
	rxreq
	txresp

	# the purge restart target, avoiding vcl_error error which closes connection.
	rxreq
	txresp

	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import softpurge from "${vmod_topbuild}/src/.libs/libvmod_softpurge.so";
	backend b1 {
		.host = "${s1_addr}";
		.port = "${s1_port}";
	}

	sub vcl_recv {
		set req.backend_hint = b1;
		
		if (req.method == "PURGE") { return(hash); }
	}

	sub vcl_backend_response {
		set beresp.ttl = 1m;
		set beresp.grace = 10m;
	}

	sub vcl_miss {
		set req.http.x-via = "miss";
	}

	sub vcl_hit {
		set req.http.x-via = "hit";

		if (req.method == "PURGE") {
			softpurge.softpurge();
			set req.url = "/exists";
			set req.method = "GET";
			unset req.http.x-via;
			return(restart);
		}
	}

	sub vcl_deliver {
		set resp.http.x-object-hits = obj.hits;
		set resp.http.x-via = req.http.x-via;
	}

} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.x-via == "miss"

	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.x-via == "hit"

	# run the softpurge
	txreq -req "PURGE" -url "/"
	rxresp

	# check that it was a miss + successful fetch.
	txreq -url "/"
	rxresp
	expect resp.http.x-via == "miss"
	expect resp.status == 200
	expect resp.http.x-object-hits == 0
} -run

