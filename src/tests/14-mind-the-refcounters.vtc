varnishtest "Test softpurge vmod"

server s1 {
       rxreq
       txresp

       accept
       rxreq
       txresp
} -start

varnish v1 -vcl+backend {
       import softpurge from "${vmod_topbuild}/src/.libs/libvmod_softpurge.so";
       backend b1 {
               .host = "${s1_addr}";
               .port = "${s1_port}";
       }

       sub vcl_recv {
               set req.backend = b1;

               if (req.request ~ "PURGE") { return(lookup); }
       }

       sub vcl_fetch {
               set beresp.ttl = 5s;
               set beresp.grace = 5s;
       }

       sub vcl_miss {
               set req.http.x-via = "miss";
       }

       sub vcl_hit {
               set req.http.x-via = "hit";

               if (req.request == "PURGE") {
                       purge;
                       error 200 "Purged";
               } else if (req.request == "SOFTPURGE") {
                       softpurge.softpurge();
                       error 200 "Softpurged";
               }
       }

       sub vcl_deliver {
               set resp.http.x-object-hits = obj.hits;
               set resp.http.x-via = req.http.x-via;
       }

} -start

client c1 {
       txreq -url "/"
       rxresp

       txreq -url "/"
       rxresp
       expect resp.status == 200
       expect resp.http.x-via == "hit"

       # run the softpurge
       txreq -req "SOFTPURGE" -url "/"
       rxresp
       expect resp.status == 200
       expect resp.msg == "Softpurged"
} -run

client c2 {
       # check that it was a miss + successful fetch.
       txreq -url "/"
       rxresp
       expect resp.http.x-via == "miss"
       expect resp.status == 200
       expect resp.http.x-object-hits == 0

       txreq -req "PURGE" -url "/"
       rxresp
       expect resp.status == 200
       expect resp.msg == "Purged"
} -run

delay 20

varnish v1 -expect n_object == 0
