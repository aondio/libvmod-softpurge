varnishtest "Test softpurge vmod"

server s1 {
	rxreq
	txresp -hdr "Vary: User-Agent" -bodylen 1

} -repeat 6 -start

varnish v1 -vcl+backend {
	import softpurge from "${vmod_topbuild}/src/.libs/libvmod_softpurge.so";
	#import std;

	sub vcl_recv {
		set req.grace = 10m;
		if (req.request == "PURGE") { return(lookup); }
	}

	sub vcl_fetch {
		set beresp.ttl = 1m;
		set beresp.grace = 10m;

		set req.http.x-ttl = beresp.ttl;
		set req.http.x-grace = beresp.grace;
	}

	sub vcl_miss {
		#std.log("inside vcl_miss");
		set req.http.x-via = "miss";
	}

	sub vcl_hit {
		#std.log("inside vcl_hit");
		set req.http.x-via = "hit";
		if (req.request == "PURGE") {
			softpurge.softpurge();

			set req.url = "/exists";
			set req.request = "GET";
			unset req.http.x-via;
			return(restart);
		}

		# There is no greater-than in the test language. (?)
		if (obj.ttl < 0s) { set req.http.x-ttl-negative = true; }
		else { set req.http.x-ttl-negative = false; }

		set req.http.x-ttl = obj.ttl;
		set req.http.x-grace = obj.grace;
	}

	sub vcl_deliver {
		set resp.http.x-object-hits = obj.hits;
		set resp.http.x-object-ttl     = req.http.x-ttl;
		set resp.http.x-object-grace   = req.http.x-grace;

		set resp.http.x-via = req.http.x-via;

		set resp.http.x-ttl-negative = req.http.x-ttl-negative;

		#std.log("YY: ttl is " + req.http.x-ttl);
		#std.log("YY: grace is " + req.http.x-grace);
	}
} -start

client c1 {
	txreq -url "/" -hdr "User-Agent: one"
	rxresp
	expect resp.status == 200
	expect resp.http.x-via == "miss"

	txreq -url "/" -hdr "User-Agent: two"
	rxresp
	expect resp.status == 200
	expect resp.http.x-via == "miss"

	txreq -req "PURGE" -url "/" -hdr "User-Agent: one"
	rxresp

	txreq -url "/" -hdr "User-Agent: one"
	rxresp
	expect resp.status == 200
	expect resp.http.x-via == "miss"

	txreq -url "/" -hdr "User-Agent: two"
	rxresp
	expect resp.status == 200
	expect resp.http.x-via == "miss"
} -run
